apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.5'
group 'com.aharoldk'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def userBintray = properties.getProperty('user.bintray')
def passwordBintray = properties.getProperty('password.bintray')

publishing {
    publications {
        Production(MavenPublication) {
            artifact("build/outputs/aar/mysdk-release.aar")
            groupId this.group
            artifactId 'mysdk'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = userBintray
    key = passwordBintray
    publications = ['Production']
    configurations = ['archives']
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'MySDK'
        name = 'com.aharoldk.mysdk'
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}